const LOCALID_Samuel = 1
const LOCALID_Lauren = 2

const LOCALID_AquaGrunt_One = 9
const LOCALID_AquaGrunt_Two = 10

mapscripts PetalburgCityAqua_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        subquestmenu(QUEST_MENU_CHECK_COMPLETE, HOMELY, PROTECT)
        if (var(VAR_RESULT) == FALSE) {
            setflag(FLAG_HIDE_MAP_NAME_POPUP)
            savebgm(MUS_PL_LOOKER)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_1, 0 {
            addvar(VAR_TEMP_1, 1)
            setmetatile(15, 8, METATILE_Petalburg_Door_Gym_Closed, TRUE)
        }
    ]
}

script PetalburgCityAqua_EventScripts_AquaGrunt_Male {
    lock
    msgbox(format("We own PETALBURG CITY!\n TEAM AQUA on top!"), MSGBOX_DEFAULT)
    closemessage
    release
}

script PetalburgCityAqua_EventScripts_AquaGrunt_Female {
    lock
    msgbox(format("TEAM AQUA is the best!\n PETALBURG CITY is ours!"),  MSGBOX_DEFAULT)
    closemessage
    release
}

script PetalburgCityAqua_EventScripts_SamuelIntro_Trigger {
    lock
    setvar(VAR_TEMP_0, 1)
    getplayerxy(VAR_TEMP_A, VAR_TEMP_B)
    switch (var(VAR_TEMP_A)) {
        case 12:
            applymovement(LOCALID_Samuel, PetalburgCityAqua_Movement_SamuelIntro_One)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_SamuelIntro_Player_One)
        case 13:
            applymovement(LOCALID_Samuel, PetalburgCityAqua_Movement_SamuelIntro_Two)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_SamuelIntro_Player_Two)
        case 14:
            applymovement(LOCALID_Samuel, PetalburgCityAqua_Movement_SamuelIntro_Three)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_SamuelIntro_Player_Three)
        case 16:
            applymovement(LOCALID_Samuel, PetalburgCityAqua_Movement_SamuelIntro_Four)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_SamuelIntro_Player_Four)
    }
    waitmovement(0)
    goto(PetalburgCityAqua_EventScripts_SamuelIntro)
}

script PetalburgCityAqua_EventScripts_Samuel {
    subquestmenu(QUEST_MENU_CHECK_COMPLETE, HOMELY, PROTECT)
    if (var(VAR_RESULT) == FALSE) {
        goto(PetalburgCityAqua_EventScripts_SamuelIntro)
    } else {
        goto(PetalburgCityAqua_EventScripts_SamuelLetsGo)
    }
}

script PetalburgCityAqua_EventScripts_SamuelIntro {
    lock
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    if (var(VAR_TEMP_0) != 1) {
        faceplayer
    }
    msgbox(format("DAD: Is this the villainous TEAM AQUA that you've encountered, {PLAYER}?\l What are they doing in PETALBURG CITY?\p Everybody seems to be inside their homes, that's good for sure…"), MSGBOX_DEFAULT)
    closemessage
    applymovement(LOCALID_Samuel, FaceDown)
    waitmovement(0)
    delay(60)
    playse(SE_PIN)
    applymovement(LOCALID_Samuel, Exclaim)
    waitmovement(0)
    if (var(VAR_TEMP_0) != 1) {
        faceplayer
    } else {
        switch (var(VAR_TEMP_A)) {
            case 12:
            case 13:
            case 14:
                applymovement(LOCALID_Samuel, FaceLeft)
            case 16:
                applymovement(LOCALID_Samuel, FaceRight)
        }
    }
    waitmovement(0)
    msgbox(format("DAD: {PLAYER}!\p We need to do something about this!\n I know we've just had a fierce battle,\l but will you help us handle TEAM AQUA?"), MSGBOX_YESNO)
    while (var(VAR_RESULT) == NO) {
        msgbox(("DAD: Please {PLAYER}, we need to drive\nTEAM AQUA out of PETALBURG CITY.\lWill you help us do it?"), MSGBOX_YESNO)
    }
    msgbox(format("DAD: Let's do it, {PLAYER}.\n But first, to the POKéMON CENTER."), MSGBOX_DEFAULT)
    closemessage
    setvar(VAR_TEMP_0, 1)
    completesubquest(HOMELY, PROTECT)
    goto(PetalburgCityAqua_EventScripts_StartAttack)
}

script PetalburgCityAqua_EventScripts_SamuelLetsGo {
    end
}

script PetalburgCityAqua_EventScripts_StartAttack {
    switch (var(VAR_TEMP_A)) {
        case 12:
        case 13:
        case 14:
            applymovement(LOCALID_Samuel, PetalburgCityAqua_Movement_StartAttack_SamuelWalk_One)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_StartAttack_PlayerWalk_One)
        case 16:
            applymovement(LOCALID_Samuel, PetalburgCityAqua_Movement_StartAttack_SamuelWalk_Three)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_StartAttack_PlayerWalk_Three)
        default:
            applymovement(LOCALID_Samuel, PetalburgCityAqua_Movement_StartAttack_SamuelWalk_Two)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_StartAttack_PlayerWalk_Two)
    }
    waitmovement(0)
    applymovement(LOCALID_AquaGrunt_One, FaceUp)
    applymovement(LOCALID_AquaGrunt_Two, FaceUp)
    waitmovement(0)
    playse(SE_PIN)
    applymovement(LOCALID_AquaGrunt_One, Exclaim)
    applymovement(LOCALID_AquaGrunt_Two, Exclaim)
    waitmovement(0)
    msgbox(format("SAMUEL: Watch out, {PLAYER}!\p Those two GRUNTS look like their getting ready to give us some trouble…\p Wait, you haven't healed your party yet. Will you be able to handle this battle?"), MSGBOX_YESNO)
    while (var(VAR_RESULT) == YES) {
        msgbox(format("SAMUEL: Are you sure?\n Can your party handle this battle?"), MSGBOX_YESNO)
    }
    msgbox(format("SAMUEL: Well you may not get\n much of a choice here."), MSGBOX_SIGN)
    special(SpawnCameraObject)
    setobjectmovementtype(LOCALID_Lauren, MOVEMENT_TYPE_FACE_DOWN)
    switch (var(VAR_TEMP_A)) {
        case 12:
        case 13:
        case 14:
            applymovement(LOCALID_Lauren, PetalburgCityAqua_Movement_StartAttack_Lauren_One)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_StartAttack_LaurenOutOfWay_One)
        default:
            applymovement(LOCALID_Lauren, PetalburgCityAqua_Movement_StartAttack_Lauren_Two)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_StartAttack_LaurenOutOfWay_Two)
    }
    waitmovement(0)
    special(RemoveCameraObject)
    goto(PetalburgCityAqua_EventScripts_AttackSetup)
}

script PetalburgCityAqua_EventScripts_AttackSetup {
    savepartypokemon
    deletepartypokemon
    givecustommon(SPECIES_DIANCIE,30,,,NATURE_RELAXED,,4,,252,,,252,31,31,31,0,31,31,MOVE_ROCK_SLIDE,MOVE_MOONBLAST,MOVE_DEFENSE_CURL,MOVE_DOUBLE_EDGE)
    givecustommon(SPECIES_INFERNAPE,31,,,NATURE_JOLLY,,2,128,,252,128,,31,31,31,31,31,31,MOVE_BLAZE_DANCE,MOVE_INFERNO,MOVE_RAGING_FURY,MOVE_HEAT_STRIKE)
    givecustommon(SPECIES_CARBINK,28,,,NATURE_SASSY,,4,,252,,,252,31,31,31,0,31,31,MOVE_ROCK_THROW,MOVE_ROLLOUT,MOVE_DEFENSE_CURL,MOVE_TACKLE)
    givecustommon(SPECIES_SWABLU,27,,,NATURE_QUIRKY,,252,,128,2,,128,31,31,31,31,31,31,MOVE_DISARMING_VOICE,MOVE_DRAGON_BREATH, MOVE_DRAGON_DANCE, MOVE_AERIAL_ACE)
    goto(PetalburgCityAqua_EventScripts_AttackOne)
}

script PetalburgCityAqua_EventScripts_AttackOne {
    msgbox(format("MOM: Stay back, {PLAYER}!\p Let your MOM handle this until we\n get you to a POKéMON CENTER.\l I'll battle in your stead!"), MSGBOX_DEFAULT)
    closemessage
    delay(60)
    playse(SE_PIN)
    applymovement(LOCALID_Samuel, Exclaim)
    applymovement(OBJ_EVENT_ID_PLAYER, Exclaim)
    waitmovement(0)
    msgbox(format("DAD: We're battling together?\n It's been a while…\l Let's get them!"), MSGBOX_DEFAULT)
    closemessage
}

movement PetalburgCityAqua_Movement_SamuelIntro_One {
    delay_16 * 3
    delay_8
    delay_4
    delay_2
    face_left
}

movement PetalburgCityAqua_Movement_SamuelIntro_Two {
    delay_16
    delay_8
    delay_4
    delay_2
    face_left
}

movement PetalburgCityAqua_Movement_SamuelIntro_Three {
    delay_8
    delay_4
    delay_2
    face_left
}

movement PetalburgCityAqua_Movement_SamuelIntro_Four {
    delay_8
    delay_4
    delay_2
    face_right
}

movement PetalburgCityAqua_Movement_SamuelIntro_Player_One {
    walk_down * 2
    walk_right * 2
}

movement PetalburgCityAqua_Movement_SamuelIntro_Player_Two {
    walk_down
    walk_right
}

movement PetalburgCityAqua_Movement_SamuelIntro_Player_Three {
    face_right
}

movement PetalburgCityAqua_Movement_SamuelIntro_Player_Four {
    walk_down
    face_left
}

movement PetalburgCityAqua_Movement_StartAttack_PlayerWalk_One {
    walk_right
    walk_down * 5
}

movement PetalburgCityAqua_Movement_StartAttack_PlayerWalk_Two {
    walk_right
    walk_down * 6
}

movement PetalburgCityAqua_Movement_StartAttack_PlayerWalk_Three {
    walk_down * 5
}

movement PetalburgCityAqua_Movement_StartAttack_SamuelWalk_One {
    walk_right
    walk_down * 5
}

movement PetalburgCityAqua_Movement_StartAttack_SamuelWalk_Two {
    delay_16
    delay_8
    face_down
    delay_1
    delay_2
    delay_4
    walk_down * 5
}

movement PetalburgCityAqua_Movement_StartAttack_SamuelWalk_Three {
    walk_down * 5
}

movement PetalburgCityAqua_Movement_StartAttack_Lauren_One {
    delay_16
    set_visible
    walk_fast_down * 5
}

movement PetalburgCityAqua_Movement_StartAttack_Lauren_Two {
    walk_right
    set_visible
    walk_fast_down * 5
}

movement PetalburgCityAqua_Movement_StartAttack_LaurenOutOfWay_One {
    delay_16 * 3
    lock_facing_direction
    walk_left
    face_down
    unlock_facing_direction
}

movement PetalburgCityAqua_Movement_StartAttack_LaurenOutOfWay_Two {
    delay_16 * 3
    lock_facing_direction
    walk_right
    face_down
    unlock_facing_direction
}