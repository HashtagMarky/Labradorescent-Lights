const LEFT = 1
const RIGHT = 2

const LOCALID_Samuel = 1
const LOCALID_Lauren = 2

const LOCALID_AquaGrunt_One = 9
const LOCALID_AquaGrunt_Two = 10

const LOCALID_AquaGrunt_OneTwo = 11
const LOCALID_AquaGrunt_TwoTwo = 12

const LOCALID_AquaGrunt_Three = 14
const LOCALID_AquaGrunt_Four = 13

const LOCALID_ItemBall = 15

mapscripts PetalburgCityAqua_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        setrespawn(HEAL_LOCATION_PETALBURG_CITY_AQUA)
        subquestmenu(QUEST_MENU_CHECK_COMPLETE, HOMELY, PROTECT)
        if (var(VAR_RESULT) == FALSE) {
            setflag(FLAG_HIDE_MAP_NAME_POPUP)
            savebgm(MUS_PL_LOOKER)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_1, 0 {
            addvar(VAR_TEMP_1, 1)
            setmetatile(15, 8, METATILE_Petalburg_Door_Gym_Closed, TRUE)
        }
    ]
}

script PetalburgCityAqua_EventScripts_AquaGrunt_Male {
    lock
    msgbox(format("We own PETALBURG CITY!\n TEAM AQUA on top!"), MSGBOX_DEFAULT)
    closemessage
    release
}

script PetalburgCityAqua_EventScripts_AquaGrunt_Female {
    lock
    msgbox(format("TEAM AQUA is the best!\n PETALBURG CITY is ours!"),  MSGBOX_DEFAULT)
    closemessage
    release
}

script PetalburgCityAqua_EventScripts_SamuelIntro_Trigger {
    lock
    setvar(VAR_TEMP_0, 1)
    getplayerxy(VAR_TEMP_A, VAR_TEMP_B)
    switch (var(VAR_TEMP_A)) {
        case 12:
            applymovement(LOCALID_Samuel, PetalburgCityAqua_Movement_SamuelIntro_One)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_SamuelIntro_Player_One)
        case 13:
            applymovement(LOCALID_Samuel, PetalburgCityAqua_Movement_SamuelIntro_Two)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_SamuelIntro_Player_Two)
        case 14:
            applymovement(LOCALID_Samuel, PetalburgCityAqua_Movement_SamuelIntro_Three)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_SamuelIntro_Player_Three)
        case 16:
            applymovement(LOCALID_Samuel, PetalburgCityAqua_Movement_SamuelIntro_Four)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_SamuelIntro_Player_Four)
    }
    waitmovement(0)
    playse(SE_SELECT)
    goto(PetalburgCityAqua_EventScripts_Samuel)
}

script PetalburgCityAqua_EventScripts_Samuel {
    subquestmenu(QUEST_MENU_CHECK_COMPLETE, HOMELY, PROTECT)
    if (var(VAR_RESULT) == FALSE) {
        goto(PetalburgCityAqua_EventScripts_SamuelIntro)
    } else {
        goto(PetalburgCityAqua_EventScripts_SamuelLookLost)
    }
}

script PetalburgCityAqua_EventScripts_SamuelIntro {
    lock
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    if (var(VAR_TEMP_0) != 1) {
        faceplayer
    }
    msgbox(format("DAD: Is this the villainous TEAM AQUA that you've encountered, {PLAYER}?\l What are they doing in PETALBURG CITY?\p Everybody seems to be inside their homes, that's good for sure…"), MSGBOX_DEFAULT)
    closemessage
    applymovement(LOCALID_Samuel, FaceDown)
    waitmovement(0)
    delay(60)
    playse(SE_PIN)
    applymovement(LOCALID_Samuel, Exclaim)
    waitmovement(0)
    if (var(VAR_TEMP_0) != 1) {
        faceplayer
    } else {
        switch (var(VAR_TEMP_A)) {
            case 12:
            case 13:
            case 14:
                applymovement(LOCALID_Samuel, FaceLeft)
            case 16:
                applymovement(LOCALID_Samuel, FaceRight)
        }
    }
    waitmovement(0)
    msgbox(format("DAD: {PLAYER}!\p We need to do something about this!\n I know we've just had a fierce battle,\l but will you help us handle TEAM AQUA?"), MSGBOX_YESNO)
    while (var(VAR_RESULT) == NO) {
        msgbox(("DAD: Please {PLAYER}, we need to drive\nTEAM AQUA out of PETALBURG CITY.\lWill you help us do it?"), MSGBOX_YESNO)
    }
    msgbox(format("DAD: Let's do it, {PLAYER}!\n But first, to the POKéMON CENTER.\l Let's get your team rested up."), MSGBOX_DEFAULT)
    closemessage
    setvar(VAR_TEMP_0, 1)
    completesubquest(HOMELY, PROTECT)
    goto(PetalburgCityAqua_EventScripts_StartAttack)
}

script PetalburgCityAqua_EventScripts_SamuelLookLost {
    lock
    clearflag(FLAG_DONT_TRANSITION_MUSIC)
    if (var(VAR_TEMP_0) != 1) {
        faceplayer
    }
    msgbox(format("DAD: Are you alright there {PLAYER}? You seem lost, was your mind elsewhere…"), MSGBOX_DEFAULT)
    closemessage
    delay(60)
    goto(PetalburgCityAqua_EventScripts_SamuelIntro)
}

script PetalburgCityAqua_EventScripts_StartAttack {
    switch (var(VAR_TEMP_A)) {
        case 12:
        case 13:
        case 14:
            applymovement(LOCALID_Samuel, PetalburgCityAqua_Movement_StartAttack_SamuelWalk_One)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_StartAttack_PlayerWalk_One)
        case 16:
            applymovement(LOCALID_Samuel, PetalburgCityAqua_Movement_StartAttack_SamuelWalk_Three)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_StartAttack_PlayerWalk_Three)
        default:
            applymovement(LOCALID_Samuel, PetalburgCityAqua_Movement_StartAttack_SamuelWalk_Two)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_StartAttack_PlayerWalk_Two)
    }
    waitmovement(0)
    special(SpawnCameraObject)
    applymovement(OBJ_EVENT_ID_CAMERA, WalkDown)
    waitmovement(0)
    special(RemoveCameraObject)
    applymovement(LOCALID_AquaGrunt_One, FaceUp)
    applymovement(LOCALID_AquaGrunt_Two, FaceUp)
    waitmovement(0)
    playse(SE_PIN)
    applymovement(LOCALID_AquaGrunt_One, Exclaim)
    applymovement(LOCALID_AquaGrunt_Two, Exclaim)
    waitmovement(0)
    msgbox(format("SAMUEL: Watch out, {PLAYER}!\p Those shady characters look like their getting ready to give us some trouble…"), MSGBOX_DEFAULT)
    closemessage
    delay(30)
    playse(SE_PIN)
    applymovement(LOCALID_Samuel, Exclaim)
    msgbox(format("Wait, you haven't healed your party yet!\p They're probably still tired from our battle, can they battle again?"), MSGBOX_YESNO)
    while (var(VAR_RESULT) == YES) {
        msgbox(format("DAD: Are you sure?\n Can your party handle this battle?"), MSGBOX_YESNO)
    }
    msgbox(format("DAD: Well you may not get\n much of a choice here.\p My team is weak too, I don't know if I can handle them both alone…"), MSGBOX_DEFAULT)
    closemessage
    special(SpawnCameraObject)
    switch (var(VAR_TEMP_A)) {
        case 12:
        case 13:
        case 14:
            setvar(VAR_TEMP_B, LEFT)
            applymovement(LOCALID_Lauren, PetalburgCityAqua_Movement_StartAttack_Lauren_One)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_StartAttack_LaurenOutOfWay_One)
        default:
            setvar(VAR_TEMP_B, RIGHT)
            applymovement(LOCALID_Lauren, PetalburgCityAqua_Movement_StartAttack_Lauren_Two)
            applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_StartAttack_LaurenOutOfWay_Two)
    }
    waitmovement(0)
    special(RemoveCameraObject)
    msgbox(format("MOM: Stay back, {PLAYER}!\p Let your MOM handle this until we\n get you to a POKéMON CENTER.\l I'll battle on your behalf!"), MSGBOX_DEFAULT)
    goto(PetalburgCityAqua_EventScripts_AttackSetup)
}

script PetalburgCityAqua_EventScripts_AttackSetup {
    savepartypokemon
    deletepartypokemon
    givecustommon(SPECIES_DIANCIE,30,,,NATURE_RELAXED,,4,,252,,,252,31,31,31,0,31,31,MOVE_ROCK_SLIDE,MOVE_MOONBLAST,MOVE_DEFENSE_CURL,MOVE_DOUBLE_EDGE)
    givecustommon(SPECIES_INFERNAPE,31,,,NATURE_JOLLY,,2,128,,252,128,,31,31,31,31,31,31,MOVE_BLAZE_DANCE,MOVE_INFERNO,MOVE_RAGING_FURY,MOVE_HEAT_STRIKE)
    givecustommon(SPECIES_SABLEYE,28,,,NATURE_RELAXED,,4,,252,,,252,31,31,31,0,31,31,MOVE_FURY_SWIPES,MOVE_FEINT_ATTACK,MOVE_CALM_MIND,MOVE_SHADOW_BALL, TRUE)
    givecustommon(SPECIES_SWABLU,27,,,NATURE_QUIRKY,,252,,128,2,,128,31,31,31,31,31,31,MOVE_DISARMING_VOICE,MOVE_DRAGON_BREATH, MOVE_DRAGON_DANCE, MOVE_AERIAL_ACE)
    goto(PetalburgCityAqua_EventScripts_AttackOne)
}

script PetalburgCityAqua_EventScripts_AttackOne {
    closemessage
    delay(30)
    playse(SE_PIN)
    applymovement(LOCALID_Samuel, Exclaim)
    applymovement(OBJ_EVENT_ID_PLAYER, Exclaim)
    waitmovement(0)
    if (var(VAR_TEMP_B) == LEFT) {
        applymovement(LOCALID_Lauren, FaceRight)
        applymovement(LOCALID_Samuel, FaceLeft)
        applymovement(OBJ_EVENT_ID_PLAYER, FaceRight)
    } else {
        applymovement(LOCALID_Samuel, FaceRight)
        applymovement(LOCALID_Lauren, FaceLeft)
        applymovement(OBJ_EVENT_ID_PLAYER, FaceLeft)
    }
    waitmovement(0)
    msgbox(format("DAD: We're battling together?\n It's been a while…\p Now, this will be some fun.\n Let's get them, my love!"), MSGBOX_DEFAULT)
    applymovement(LOCALID_Samuel, FaceDown)
    applymovement(LOCALID_Lauren, FaceDown)
    applymovement(OBJ_EVENT_ID_PLAYER, FaceDown)
    waitmovement(0)
    setflag(FLAG_PARENTS_BACK_PIC)
    closemessage
    delay(30)
    msgbox(format("GRUNT 1: Even though you ran all the way here, you still can't stop us!"), MSGBOX_DEFAULT)
    closemessage
    delay(30)
    trainerbattle(TRAINER_BATTLE_CONTINUE_SCRIPT_DOUBLE_NO_MUSIC, TRAINER_GRUNT_ATTACK_1, 9, PetalburgCityAqua_Text_AttackOne_Intro, PetalburgCityAqua_Text_AttackOne_Defeat, PetalburgCityAqua_Text_AttackOne_NoPokemon, PetalburgCityAqua_EventScripts_AttackTwo)
}

script PetalburgCityAqua_EventScripts_AttackTwo {
    //special(HealPlayerParty)
    msgbox(format("GRUNT 1: You were right!\n Let's get out of here!"), MSGBOX_DEFAULT)
    closemessage
    delay(30)
    msgbox(format("GRUNT 2: The POKéMON CENTER is still shut, the next team can avenge us."), MSGBOX_DEFAULT)
    closemessage
    applymovement(LOCALID_AquaGrunt_One, PetalburgCityAqua_Movement_Grunts_RunAway)
    applymovement(LOCALID_AquaGrunt_Two, PetalburgCityAqua_Movement_Grunts_RunAway)
    waitmovement(0)
    if (var(VAR_TEMP_B) == LEFT) {
        applymovement(LOCALID_Lauren, FaceRight)
        applymovement(LOCALID_Samuel, FaceLeft)
        applymovement(OBJ_EVENT_ID_PLAYER, FaceRight)
    } else {
        applymovement(LOCALID_Samuel, FaceRight)
        applymovement(LOCALID_Lauren, FaceLeft)
        applymovement(OBJ_EVENT_ID_PLAYER, FaceLeft)
    }
    waitmovement(0)
    delay(30)
    msgbox(format("MOM: Now wasn't that alot of fun!\n I may have to start battling again."), MSGBOX_DEFAULT)
    closemessage
    delay(30)
    msgbox(format("DAD: It really was amazing!\n I look forward to doing it more.\l And actually, we may have to!\p They said the POKéMON CENTER is shut, so if you don't mind {PLAYER},\l I'll handle this with your MOM."), MSGBOX_DEFAULT)
    closemessage
    if (var(VAR_TEMP_B) == LEFT) {
        applymovement(LOCALID_Lauren, WalkRight)
        applymovement(LOCALID_Samuel, WalkRight)
        applymovement(OBJ_EVENT_ID_PLAYER, WalkRight)
        waitmovement(0)
    }
    applymovement(LOCALID_Lauren, PetalburgCityAqua_Movement_StartAttackTwo)
    applymovement(LOCALID_Samuel, PetalburgCityAqua_Movement_StartAttackTwo)
    applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_Movement_StartAttackTwo)
    waitmovement(0)
    playse(SE_PIN)
    applymovement(LOCALID_AquaGrunt_Three, Exclaim)
    applymovement(LOCALID_AquaGrunt_Four, Exclaim)
    waitmovement(0)
    msgbox(format("GRUNT 3: So it was you guys who\n beat those other two GRUNTS.\l We'll make you regret it."), MSGBOX_DEFAULT)
    closemessage
    delay(30)
    trainerbattle(TRAINER_BATTLE_CONTINUE_SCRIPT_DOUBLE_NO_MUSIC, TRAINER_GRUNT_ATTACK_2, 13, PetalburgCityAqua_Text_AttackTwo_Intro, PetalburgCityAqua_Text_AttackTwo_Defeat, PetalburgCityAqua_Text_AttackTwo_NoPokemon, PetalburgCityAqua_EventScripts_RetreatToPassage)
}

script PetalburgCityAqua_EventScripts_RetreatToPassage {
    msgbox(format("GRUNT 4: The BOSS isn't gonna be happy, I'm not looking forward to telling him."), MSGBOX_DEFAULT)
    closemessage
    applymovement(LOCALID_ItemBall, PetalburgCityAqua_Movement_ItemBallAppear)
    applymovement(LOCALID_AquaGrunt_OneTwo, PetalburgCityAqua_Movement_GruntOne_RunToPassage)
    applymovement(LOCALID_AquaGrunt_TwoTwo, PetalburgCityAqua_Movement_GruntTwo_RunToPassage)
    applymovement(LOCALID_AquaGrunt_Three, PetalburgCityAqua_Movement_GruntThree_RunToPassage)
    applymovement(LOCALID_AquaGrunt_Four, PetalburgCityAqua_Movement_GruntFour_RunToPassage)
    waitmovement(LOCALID_AquaGrunt_Four)
    if (var(VAR_TEMP_B) == LEFT) {
        applymovement(LOCALID_Lauren, FaceRight)
        applymovement(LOCALID_Samuel, FaceLeft)
        applymovement(OBJ_EVENT_ID_PLAYER, FaceRight)
    } else {
        applymovement(LOCALID_Samuel, FaceRight)
        applymovement(LOCALID_Lauren, FaceLeft)
        applymovement(OBJ_EVENT_ID_PLAYER, FaceLeft)
    }
    waitmovement(0)
    msgbox(format("MOM: That seems like the last of them."), MSGBOX_DEFAULT)
    closemessage
    delay(30)
    msgbox(format("DAD: Those folks needed to be stopped, I'm glad that I could help!"), MSGBOX_DEFAULT)
    closemessage
    applymovement(LOCALID_Lauren, FaceDown)
    waitmovement(0)
    playse(SE_PIN)
    applymovement(LOCALID_Lauren, Exclaim)
    msgbox(format("DAD: Look at that over there!\n What is that?"), MSGBOX_DEFAULT)
    waitmovement(0)
    closemessage
    applymovement(LOCALID_Samuel, FaceDown)
    applymovement(OBJ_EVENT_ID_PLAYER, FaceDown)
    waitmovement(0)
    delay(30)
    playse(SE_PIN)
    applymovement(LOCALID_Samuel, Exclaim)
    applymovement(OBJ_EVENT_ID_PLAYER, Exclaim)
    waitmovement(0)
    msgbox(format("MOM: Those GRUNTS must've dropped it.\n Go on {PLAYER}, go see what it is."), MSGBOX_DEFAULT)
    closemessage
    special(SpawnCameraObject)
    if (var(VAR_TEMP_B) == LEFT) {
        applymovement(LOCALID_Lauren, PetalburgCityAqua_BlockSeeItem_Left)
        applymovement(LOCALID_Samuel, PetalburgCityAqua_BlockSeeItem_Left)
        applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_GoSeeItem_Left)
        applymovement(OBJ_EVENT_ID_CAMERA, PetalburgCityAqua_CameraSeeItem_Left)
    } else {
        applymovement(LOCALID_Samuel, PetalburgCityAqua_BlockSeeItem_Right)
        applymovement(LOCALID_Lauren, PetalburgCityAqua_BlockSeeItem_Right)
        applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_GoSeeItem_Right)
        applymovement(OBJ_EVENT_ID_CAMERA, PetalburgCityAqua_CameraSeeItem_Right)
    }
    waitmovement(OBJ_EVENT_ID_PLAYER)
    goto(PetalburgCityAqua_EventScripts_HealTeam)
}

script PetalburgCityAqua_EventScripts_HealTeam {
    giveitem(ITEM_MAX_REVIVE, 6)
    closemessage
    delay(30)
    giveitem(ITEM_FULL_RESTORE, 6)
    closemessage
    delay(30)
    applymovement(LOCALID_ItemBall, PetalburgCityAqua_HideItemBall)
    applymovement(OBJ_EVENT_ID_PLAYER, FaceUp)
    waitmovement(LOCALID_ItemBall)
    msgbox(format("MOM: Oh wow, who needs a POKéMON CENTER with items like this, {PLAYER}, use these items to heal your team."), MSGBOX_DEFAULT)
    closemessage
    fadescreen(FADE_TO_BLACK)
	playfanfare(MUS_DP_HEAL)
    loadpartypokemon
    clearflag(FLAG_PARENTS_BACK_PIC)
	special(HealPlayerParty)
    removeitem(ITEM_MAX_REVIVE, 6)
    removeitem(ITEM_FULL_RESTORE, 6)
    clearflag(FLAG_HIDE_MAP_NAME_POPUP)
	waitfanfare
    fadescreen(FADE_FROM_BLACK)
    msgbox(format("MOM: I can't believe that I'm going to suggest this, but now you're ready to battle, you should follow TEAM AQUA!\p Your DAD and I can make sure PETALBURG CITY is rid of the members that remain."), MSGBOX_DEFAULT)
    closemessage
    if (var(VAR_TEMP_B) == LEFT) {
        applymovement(LOCALID_Samuel, FaceLeft)
    } else {
        applymovement(LOCALID_Samuel, FaceRight)
    }
    waitmovement(0)
    playse(SE_PIN)
    applymovement(LOCALID_Samuel, Exclaim)
    delay(90)
    applymovement(LOCALID_Samuel, FaceDown)
    msgbox(format("DAD: I can't believe she suggested it either, but I think your MOM is right.\p Go stop TEAM AQUA from causing any more damage in BOREA PASSAGE!"), MSGBOX_DEFAULT)
    closemessage
    delay(30)
    playfanfare(MUS_DP_LEVEL_UP)
    waitfanfare
    applymovement(OBJ_EVENT_ID_PLAYER, PetalburgCityAqua_EnterPassage)
    waitmovement(0)
    warp(MAP_BOREA_PASSAGE1F, 2)
    waitstate
}

script PetalburgCityAqua_EventScripts_SamuelDefendPetalburg {
    msgbox(format("DAD: Go on {PLAYER}!\n Go and stop TEAM AQUA!"), MSGBOX_DEFAULT)
    closemessage
}

script PetalburgCityAqua_EventScripts_LaurenDefendPetalburg {
    msgbox(format("MOM: Go on {PLAYER}!\n We'll look after the people of PETALBUG!"), MSGBOX_DEFAULT)
    closemessage
}

// give max revives, give full restores then take them away to show that they've been picked up and used.

text PetalburgCityAqua_Text_AttackOne_Intro {
    format("GRUNT 2: Wait, isn't that the GYM\n LEADER around these parts?\l And his wife to boot?")
}

text PetalburgCityAqua_Text_AttackOne_Defeat {
    format("GRUNT 1: I guess you two really\n make a superbly good team…\p GRUNT 2: I'm just jealous that you\n two can work so well together…")
}

text PetalburgCityAqua_Text_AttackOne_NoPokemon {
    format("GRUNT 1: You're right!\n Let's get out of here!")
}

text PetalburgCityAqua_Text_AttackTwo_Intro {
    format("GRUNT 4: We're not gonna let you into the BOREA PASSAGE and stop the BOSS!")
}

text PetalburgCityAqua_Text_AttackTwo_Defeat {
    format("GRUNT 3: Well no wonder you\n beat the other two…\p GRUNT 4: Yeah…\n You're a great team…")
}

text PetalburgCityAqua_Text_AttackTwo_NoPokemon {
    format("GRUNT 4: No, the BOSS wants us\n to retreat into BOREA PASSAGE.\l Let's ditch these guys.")
}

movement PetalburgCityAqua_Movement_SamuelIntro_One {
    delay_16 * 3
    delay_8
    delay_4
    delay_2
    face_left
}

movement PetalburgCityAqua_Movement_SamuelIntro_Two {
    delay_16
    delay_8
    delay_4
    delay_2
    face_left
}

movement PetalburgCityAqua_Movement_SamuelIntro_Three {
    delay_8
    delay_4
    delay_2
    face_left
}

movement PetalburgCityAqua_Movement_SamuelIntro_Four {
    delay_8
    delay_4
    delay_2
    face_right
}

movement PetalburgCityAqua_Movement_SamuelIntro_Player_One {
    walk_down * 2
    walk_right * 2
}

movement PetalburgCityAqua_Movement_SamuelIntro_Player_Two {
    walk_down
    walk_right
}

movement PetalburgCityAqua_Movement_SamuelIntro_Player_Three {
    face_right
}

movement PetalburgCityAqua_Movement_SamuelIntro_Player_Four {
    walk_down
    face_left
}

movement PetalburgCityAqua_Movement_StartAttack_PlayerWalk_One {
    walk_right
    walk_down * 5
}

movement PetalburgCityAqua_Movement_StartAttack_PlayerWalk_Two {
    walk_right
    walk_down * 6
}

movement PetalburgCityAqua_Movement_StartAttack_PlayerWalk_Three {
    walk_down * 5
}

movement PetalburgCityAqua_Movement_StartAttack_SamuelWalk_One {
    walk_right
    walk_down * 5
}

movement PetalburgCityAqua_Movement_StartAttack_SamuelWalk_Two {
    delay_16
    delay_8
    face_down
    delay_1
    delay_2
    delay_4
    walk_down * 5
}

movement PetalburgCityAqua_Movement_StartAttack_SamuelWalk_Three {
    walk_down * 5
}

movement PetalburgCityAqua_Movement_StartAttack_Lauren_One {
    delay_16
    set_visible
    walk_fast_down * 5
}

movement PetalburgCityAqua_Movement_StartAttack_Lauren_Two {
    walk_right
    set_visible
    walk_fast_down * 5
}

movement PetalburgCityAqua_Movement_StartAttack_LaurenOutOfWay_One {
    delay_16 * 3
    lock_facing_direction
    walk_left
    face_down
    unlock_facing_direction
}

movement PetalburgCityAqua_Movement_StartAttack_LaurenOutOfWay_Two {
    delay_16 * 3
    lock_facing_direction
    walk_right
    face_down
    unlock_facing_direction
}

movement PetalburgCityAqua_Movement_Grunts_RunAway {
    walk_fast_down * 5
    set_invisible
}

movement PetalburgCityAqua_Movement_StartAttackTwo {
    walk_down * 15
}

movement PetalburgCityAqua_Movement_GruntOne_RunToPassage {
    walk_fast_right
    walk_fast_up
    delay_16
    set_invisible
    walk_faster_up * 2
}

movement PetalburgCityAqua_Movement_GruntTwo_RunToPassage {
    walk_fast_right
    walk_fast_up
    delay_16
    walk_fast_up
    delay_16
    set_invisible
    walk_faster_up * 2
}

movement PetalburgCityAqua_Movement_GruntThree_RunToPassage {
    walk_fast_down * 2
    walk_fast_left * 5
    walk_fast_up
    delay_16
    set_invisible
    walk_faster_up * 2
}

movement PetalburgCityAqua_Movement_GruntFour_RunToPassage {
    walk_fast_down * 2
    walk_fast_left * 6
    face_up
    delay_16
    walk_fast_up
    delay_16
    set_invisible
    walk_faster_up * 2
}

movement PetalburgCityAqua_Movement_ItemBallAppear {
    delay_16
    delay_4
    set_visible
}

movement PetalburgCityAqua_GoSeeItem_Left {
    walk_down * 3
}

movement PetalburgCityAqua_GoSeeItem_Right {
    walk_down
    walk_left * 2
    walk_down * 2
}

movement PetalburgCityAqua_CameraSeeItem_Left {
    walk_diag_southwest * 2
}

movement PetalburgCityAqua_CameraSeeItem_Right {
    walk_diag_southwest * 2
}

movement PetalburgCityAqua_BlockSeeItem_Left {
    walk_left
    walk_down * 2
}

movement PetalburgCityAqua_BlockSeeItem_Right {
    delay_16 * 3
    walk_down * 2
}

movement PetalburgCityAqua_HideItemBall {
    set_invisible
    walk_faster_down * 3
}

movement PetalburgCityAqua_EnterPassage {
    walk_down
    walk_left * 5
    walk_up
    delay_16
    set_invisible
}