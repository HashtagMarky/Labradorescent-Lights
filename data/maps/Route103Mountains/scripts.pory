raw `
Route103Mountains_MapScripts::
	map_script MAP_SCRIPT_ON_LOAD, Route103Mountains_MapScripts_OnLoad
	map_script MAP_SCRIPT_ON_TRANSITION, Route103Mountains_EventScript_OnTransition
	.byte 0
	
Route103Mountains_MapScripts_OnLoad:
	call_if_set FLAG_SYS_DEMO_MODE, Route103Mountains_EventScript_DemoMetatile
	return

Route103Mountains_EventScript_DemoMetatile:
	setmetatile 15, 15, METATILE_General_RockWall_RockBase, TRUE
	setmetatile 15, 14, METATILE_General_RockWall_RockBase, TRUE
	setmetatile 7, 19, METATILE_General_RockWall_RockBase, TRUE
	setmetatile 7, 20, METATILE_General_RockWall_GrassBase, TRUE
	special DrawWholeMapView
	end

Route103Mountains_EventScript_OnTransition:
	special GetTimeOfDay
	goto_if_eq VAR_RESULT, 0, Route103Mountains_OnTransition_Day
	goto_if_eq VAR_RESULT, 1, Route103Mountains_OnTransition_Twilight
	goto_if_eq VAR_RESULT, 2, Route103Mountains_OnTransition_Night
	end

Route103Mountains_OnTransition_Day:
	setvar VAR_TIMEOFDAY, 0
	end

Route103Mountains_OnTransition_Twilight:
	setvar VAR_TIMEOFDAY, 1
	end

Route103Mountains_OnTransition_Night:
	setvar VAR_TIMEOFDAY, 2
	end
`
const LOCALID_Ignatius = 1
const LOCALID_Samuel = 6
const LOCALID_Lauren = 7
const LOCALID_QueenAbsol = 8

script Homely_Route103Mountains_Outside_SamuelLauren {
    msgbox(format("MOM: You sent our child to do what-?!"), MSGBOX_SIGN)
    delay(60)
    msgbox(format("DAD: {PLAYER} is about to challenge the PETALBURG CITY GYM.\p Doing so with an extraordinary team.\n Plus IGGY went up there as well.\l They will be more than fine…"), MSGBOX_SIGN)
    if (flag(FLAG_HIDE_QUEEN_ABSOL) == TRUE) {
        playse(SE_PIN)
        setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
        applymovement(LOCALID_Samuel, Homely_LookUp_Exclaim)
        applymovement(LOCALID_Lauren, Homely_LookUp_Exclaim)
        waitse
        waitmovement(0)
        msgbox(format("DAD: Ah look, {PLAYER} is here just in time to prove me right."), MSGBOX_DEFAULT)
        closemessage
        delay(30)
        msgbox(format("MOM: Oh my {PLAYER}, are you okay?"), MSGBOX_DEFAULT)
        closemessage
        applymovement(LOCALID_Samuel, Homely_Talk_FacingUp)
        applymovement(LOCALID_Lauren, Homely_Talk_FacingUp)
        applymovement(OBJ_EVENT_ID_PLAYER, Homely_Talk_FacingDown)
        waitmovement(0)
        msgbox(format("MOM: Wow… I don't believe it…\n Your DAD… He… He was…\l He was right… Just wow!\p Well, I'm glad to hear it!\n I should've had more faith in you.\l You even have new company with you."), MSGBOX_DEFAULT)
        closemessage
        delay(30)
        msgbox(format("DAD: Well, this will be interesting.\n IGGY told me about this POKéMON…\p It must be a sign of some BAD OMEN\n that will occur in the area.\l I hope I'm wrong…\p Regardless…\n You're really coming along {PLAYER}.\l I'm excited for our battle.\p Speaking of, it's time we be off.\n We'll see you in PETALBURG CITY.\l For sure this time."), MSGBOX_DEFAULT)
        closemessage
        completequest(CALAMITY_QUEEN)
        setrespawn(HEAL_LOCATION_OLDALE_TOWN)
        setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
        applymovement(LOCALID_Samuel, Homely_Oldale_LaurenSamuel_Leave)
        applymovement(LOCALID_Lauren, Homely_Oldale_LaurenSamuel_Leave)
        fadescreen(FADE_TO_BLACK)
        waitmovement(0)
	    playfanfare(MUS_DP_HEAL)
        removeobject(LOCALID_Samuel)
        removeobject(LOCALID_Lauren)
	    waitfanfare
	    special(HealPlayerParty)
        callnative(UpdateFollowingPokemon)
        setflag(FLAG_HIDE_OLDALE_FAMILY)
        clearflag(FLAG_HIDE_PETALBURG_FAMILY)
        setvar(VAR_FLYING_TAXI, 2)
        setvar(VAR_LAB_LIGHTS, 27)
	    fadescreen(FADE_FROM_BLACK)
        completesubquest(HOMELY, PROTECT)
        releaseall
    }
}

script Homely_Route103Mountains_QueenAbsol_Encounter {
    if (flag(FLAG_HIDE_QUEEN_ABSOL) == FALSE) {
        setvar(VAR_LAB_LIGHTS, 0)
        setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
        delay(8)
        applymovement(OBJ_EVENT_ID_FOLLOWER, FaceUp)
        waitmovement(0)
        fadeoutbgm(2)
        special(SpawnCameraObject)
        applymovement(OBJ_EVENT_ID_CAMERA, Homely_QueenAbsol_CameraPan)
        waitmovement(0)
        playmoncry(SPECIES_QUEEN_ABSOL, CRY_MODE_ENCOUNTER)
        applymovement(LOCALID_QueenAbsol, Homely_QueenAbsolJump)
        waitmovement(0)
        waitmoncry
        delay(30)
        setwildbattle(SPECIES_QUEEN_ABSOL, 30, ITEM_SCARY_CHARM)
        setflag(FLAG_SYS_NO_CATCHING)
        special(RemoveCameraObject)
        dowildbattle
        clearflag(FLAG_SYS_NO_CATCHING)
        special(SpawnCameraObject)
        applymovement(OBJ_EVENT_ID_FOLLOWER, EnterPokeballMovement)
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, Homely_QueenAbsol_PlayerApproach)
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_CAMERA, Homely_QueenAbsol_CameraRevert)
        waitmovement(0)
        special(RemoveCameraObject)
        delay(30)
        playmoncry(SPECIES_QUEEN_ABSOL, 0)
        addobject(LOCALID_Ignatius)
        setobjectxy(LOCALID_Ignatius, 8, 11)
        applymovement(LOCALID_Ignatius, Homely_Route103Mountains_Ignatius_Approach)
        applymovement(OBJ_EVENT_ID_PLAYER, Homely_Route103Mountains_Ignatius_ApproachPlayer)
        waitmovement(0)
        msgbox(format("PROF. IGNATIUS: Well well well…\n Look here, if it isn't {PLAYER}.\p Many people have been looking for this POKéMON, the elusive QUEEN ABSOL.\p Many people say she will be the bringer of a major CALAMITY across HOENN.\l Although, that may not be the case…\p Correlation is not always causation.\n People would be wise to remember that…\p ABSOL are well known for bringing CALAMITY to all those who see them, however, I've done the research.\p It is quite the contrary!\p Long ago, before humans could predict occurances like earthquakes or tidal waves, ABSOL would warn victims.\p The horn on its head can sense these occurances well ahead of time.\p They would appear to warn people, so that they could protect themselves.\p More recently people have confused the\n appearance of ABSOL as being the root cause of all CALAMITY that occurs.\p Though, the appearance of the QUEEN ABSOL shows that there may well be impending catastrophic danger.\p She seems to repesct you, {PLAYER}…\p Maybe she will allow you to help her prevent it from occuring."), MSGBOX_SIGN)
        delay(30)
        applymovement(OBJ_EVENT_ID_PLAYER, FaceUp)
        waitmovement(0)
        playmoncry(SPECIES_QUEEN_ABSOL, 0)
        msgbox(format("ABSOL seems to bow toward you…\n Would you like to use a POKé BALL?"), MSGBOX_YESNO)
        while (var(VAR_RESULT) == NO) {
            msgbox(format("The QUEEN ABSOL seems certain!\n Would you like to use a POKé BALL?"), MSGBOX_YESNO)
        }
        waitmoncry
        fadescreen(FADE_TO_BLACK)
        givecustommon(SPECIES_QUEEN_ABSOL, 30,,,NATURE_ADAMANT,,252,252,,4,,,31,31,,31,,,MOVE_SWORDS_DANCE,MOVE_BITE,MOVE_QUICK_ATTACK,MOVE_CALAMITY,FALSE)
        playmoncry(SPECIES_QUEEN_ABSOL, 0)
        switch (var(VAR_RESULT)) {
            case MON_GIVEN_TO_PARTY:
                message(format("ABSOL has joined your PARTY!"))
            case MON_GIVEN_TO_PC:
                message(format("ABSOL has been sent to your PC!"))
            case MON_CANT_GIVE:
                message(format("ABSOL tried to join your team but was unable to."))
        }
        waitmoncry
        delay(60)
        closemessage
        removeobject(LOCALID_QueenAbsol)
        applymovement(OBJ_EVENT_ID_PLAYER, FaceDown)
        fadescreen(FADE_FROM_BLACK)
        waitmovement(0)
        msgbox(format("PROF IGNATIUS: Take care of ABSOL, many people would not be so kind.\l See you in PETALBURG CITY, {PLAYER}."), MSGBOX_SIGN)
        applymovement(LOCALID_Ignatius, Homely_Route103Mountains_Ignatius_Leave)
        waitmovement(0)
        removeobject(LOCALID_Ignatius)
        releaseall
        end
    } else {
        special(GetPlayerFacingDirection)
        if (var(VAR_RESULT) == DIR_NORTH) {
            applymovement(OBJ_EVENT_ID_PLAYER, WalkUp)
        } else {
            applymovement(OBJ_EVENT_ID_PLAYER, WalkDown)
        }
    }
}

movement Homely_LookUp_Exclaim {
    face_up
    emote_exclamation_mark
}

movement Homely_QueenAbsolJump {
    delay_16
    jump_in_place_down
    delay_8
    jump_in_place_down
    delay_16
}

movement Homely_QueenAbsol_CameraPan {
    walk_up * 3
}

movement Homely_QueenAbsol_CameraRevert {
    delay_16
    walk_down
}

movement Homely_QueenAbsol_PlayerApproach {
    walk_up * 2
}

movement Homely_Oldale_LaurenSamuel_Leave {
    walk_down * 3
}

movement Homely_Route103Mountains_Ignatius_Approach {
    walk_up
    walk_left
    walk_up * 2
    walk_left * 2
    walk_up * 2
}

movement Homely_Route103Mountains_Ignatius_ApproachPlayer {
    delay_16 * 8
    face_down
}

movement Homely_Route103Mountains_Ignatius_Leave {
    walk_down * 2
    walk_right * 2
    walk_down * 2
    walk_right
    walk_down 
}