raw `
Route103Mountains_MapScripts::
	map_script MAP_SCRIPT_ON_LOAD, Route103Mountains_MapScripts_OnLoad
	map_script MAP_SCRIPT_ON_TRANSITION, Route103Mountains_EventScript_OnTransition
	.byte 0
	
Route103Mountains_MapScripts_OnLoad:
	call_if_set FLAG_SYS_DEMO_MODE, Route103Mountains_EventScript_DemoMetatile
	return

Route103Mountains_EventScript_DemoMetatile:
	setmetatile 15, 15, METATILE_General_RockWall_RockBase, TRUE
	setmetatile 15, 14, METATILE_General_RockWall_RockBase, TRUE
	setmetatile 7, 19, METATILE_General_RockWall_RockBase, TRUE
	setmetatile 7, 20, METATILE_General_RockWall_GrassBase, TRUE
	special DrawWholeMapView
	end

Route103Mountains_EventScript_OnTransition:
	special GetTimeOfDay
	goto_if_eq VAR_RESULT, 0, Route103Mountains_OnTransition_Day
	goto_if_eq VAR_RESULT, 1, Route103Mountains_OnTransition_Twilight
	goto_if_eq VAR_RESULT, 2, Route103Mountains_OnTransition_Night
	end

Route103Mountains_OnTransition_Day:
	setvar VAR_TIMEOFDAY, 0
	end

Route103Mountains_OnTransition_Twilight:
	setvar VAR_TIMEOFDAY, 1
	end

Route103Mountains_OnTransition_Night:
	setvar VAR_TIMEOFDAY, 2
	end
`

const LOCALID_Samuel = 6
const LOCALID_Lauren = 7
const LOCALID_QueenAbsol = 8

script Homely_Route103Mountains_Outside_SamuelLauren {
    msgbox(format("MOM: You sent our child to do what-?!"), MSGBOX_SIGN)
    delay(60)
    msgbox(format("DAD: {PLAYER} is about to challenge the PETALBURG CITY GYM.\p Doing so with an extraordinary team.\n They will be more than fine…"), MSGBOX_SIGN)
    if (flag(FLAG_HIDE_QUEEN_ABSOL) == TRUE) {
        playse(SE_PIN)
        setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
        applymovement(LOCALID_Samuel, Homely_LookUp_Exclaim)
        applymovement(LOCALID_Lauren, Homely_LookUp_Exclaim)
        waitse
        waitmovement(0)
        msgbox(format("DAD: Ah look, {PLAYER} is here to prove me right."), MSGBOX_DEFAULT)
        closemessage
        delay(30)
        msgbox(format("MOM: Oh my gosh {PLAYER}, are you okay?"), MSGBOX_DEFAULT)
        applymovement(LOCALID_Samuel, Homely_Talk_FacingUp)
        applymovement(LOCALID_Lauren, Homely_Talk_FacingUp)
        applymovement(OBJ_EVENT_ID_PLAYER, Homely_Talk_FacingDown)
        waitmovement(0)
        msgbox(format("MOM: Wow…\n Your DAD…\l Was right…\p Well, I'm glad to hear it!\n I should've had more faith in you.\l You even have new company with you."), MSGBOX_DEFAULT)
        closemessage
        delay(30)
        msgbox(format("DAD: Well, this will be interesting.\n You're really coming along {PLAYER}.\l I'm excited for our battle.\p Speaking of, it's time we be off.\n See you in PETALBURG CITY, {PLAYER}.\l For sure this time."), MSGBOX_DEFAULT)
        closemessage
        completequest(CALAMITY_QUEEN)
        fadescreen(FADE_TO_BLACK)
	    playfanfare(MUS_DP_HEAL)
        removeobject(LOCALID_Samuel)
        removeobject(LOCALID_Lauren)
	    waitfanfare
	    special(HealPlayerParty)
        callnative(UpdateFollowingPokemon)
	    fadescreen(FADE_FROM_BLACK)
    }
}

script Homely_Route103Mountains_QueenAbsol_Encounter {
    if (flag(FLAG_HIDE_QUEEN_ABSOL) == FALSE) {
        setvar(VAR_LAB_LIGHTS, 0)
        setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
        delay(8)
        applymovement(OBJ_EVENT_ID_FOLLOWER, FaceUp)
        waitmovement(0)
        fadeoutbgm(2)
        special(SpawnCameraObject)
        applymovement(OBJ_EVENT_ID_CAMERA, Homely_QueenAbsol_CameraPan)
        waitmovement(0)
        applymovement(LOCALID_QueenAbsol, Homely_QueenAbsolJump)
        waitmovement(0)
        playmoncry(SPECIES_QUEEN_ABSOL, 0)
        waitmoncry
        delay(30)
        setwildbattle(SPECIES_QUEEN_ABSOL, 30, ITEM_SCARY_CHARM)
        setflag(FLAG_SYS_NO_CATCHING)
        special(RemoveCameraObject)
        dowildbattle
        clearflag(FLAG_SYS_NO_CATCHING)
        special(SpawnCameraObject)
        applymovement(OBJ_EVENT_ID_FOLLOWER, EnterPokeballMovement)
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_PLAYER, Homely_QueenAbsol_PlayerApproach)
        waitmovement(0)
        applymovement(OBJ_EVENT_ID_CAMERA, Homely_QueenAbsol_CameraRevert)
        waitmovement(0)
        special(RemoveCameraObject)
        releaseall
        end
    } else {
        special(GetPlayerFacingDirection)
        if (var(VAR_RESULT) == DIR_NORTH) {
            applymovement(OBJ_EVENT_ID_PLAYER, WalkUp)
        } else {
            applymovement(OBJ_EVENT_ID_PLAYER, WalkDown)
        }
    }
}

movement Homely_LookUp_Exclaim {
    face_up
    emote_exclamation_mark
}

movement Homely_QueenAbsolJump {
    delay_16
    jump_in_place_down
    delay_8
    jump_in_place_down
    delay_16
}

movement Homely_QueenAbsol_CameraPan {
    walk_up * 3
}

movement Homely_QueenAbsol_CameraRevert {
    delay_16
    walk_down
}

movement Homely_QueenAbsol_PlayerApproach {
    walk_up * 2
}