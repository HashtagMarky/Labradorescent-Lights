const LOCALID_Samuel    = 1
const LOCALID_Infernape = 2
const LEFT              = 7
const MIDDLE            = 8
const RIGHT             = 9

mapscripts HomelyJourney_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        gettime
        copyvar(VAR_RESULT, VAR_0x8000)
        if (var(VAR_RESULT) < 13) {
            initclock(13,0)
        } elif (var(VAR_RESULT) >= 21) {
            initclock(8,0)
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_1, 0 {
            addvar(VAR_TEMP_1, 1)
            setmetatile(8, 6, METATILE_Petalburg_Door_Gym_Closed, TRUE)
            setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
        }
    ]
}

script Homely_EventScript_Trigger {
    setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
    getplayerxy(VAR_TEMP_A, VAR_TEMP_1)
    if (var(VAR_TEMP_A) == LEFT) {
        applymovement(LOCALID_Samuel, Homely_Petalburg_Samuel_TalkToSamuel_Left)
        applymovement(LOCALID_Infernape, Homely_Petalburg_Infernape_TalkToSamuel_Left)
        applymovement(OBJ_EVENT_ID_PLAYER, Homely_Petalburg_Player_TalkToSamuel_Left)
    } elif (var(VAR_TEMP_A) == MIDDLE) {
        applymovement(LOCALID_Samuel, Homely_Petalburg_Samuel_TalkToSamuel_Middle)
        applymovement(LOCALID_Infernape, Homely_Petalburg_Infernape_TalkToSamuel_Middle)
        applymovement(OBJ_EVENT_ID_PLAYER, Homely_Petalburg_Player_TalkToSamuel_Middle)
    } elif (var(VAR_TEMP_A) == RIGHT) {
        applymovement(LOCALID_Samuel, Homely_Petalburg_Samuel_TalkToSamuel_Right)
        applymovement(LOCALID_Infernape, Homely_Petalburg_Infernape_TalkToSamuel_Right)
        applymovement(OBJ_EVENT_ID_PLAYER, Homely_Petalburg_Player_TalkToSamuel_Right)
    }
    waitmovement(0)
    msgbox(format("SAMUEL: Come on, {PLAYER}.\n We have a nice walk ahead of us."), MSGBOX_DEFAULT)
    closemessage
    goto(Homely_EventScript_Petalburg_Leave)
}

script Homely_EventScript_Petalburg_Leave {
    applymovement(LOCALID_Samuel, Homely_Petalburg_Samuel_Leave)
    applymovement(LOCALID_Infernape, Homely_Petalburg_Infernape_Leave)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_Petalburg_Player_Leave)
    waitmovement(0)
    msgbox(format("SAMUEL: Here we are {PLAYER}, almost at ROUTE 102. Can you remember when\l I sent you on this road with WALLY?\l You've come a long way since thenâ€¦"), MSGBOX_DEFAULT)
    closemessage
    special(SpawnCameraObject)
    applymovement(LOCALID_Samuel, Homely_Petalburg_Fadescreen)
    applymovement(LOCALID_Infernape, Homely_Petalburg_Fadescreen)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_Petalburg_Fadescreen)
    fadescreenspeed(FADE_TO_BLACK, 4)
    waitmovement(0)
    goto(Homely_EventScript_MoveToRoute102)
}

script Homely_EventScript_MoveToRoute102 {
    applymovement(OBJ_EVENT_ID_CAMERA, Homely_Petalburg_Camera_Fadescreen_Reset)
    waitmovement(0)
    special(RemoveCameraObject)
    applymovement(LOCALID_Samuel, Homely_MoveToRoute102_Samuel)
    applymovement(LOCALID_Infernape, Homely_MoveToRoute102_Infernape)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_MoveToRoute102_Player)
    waitmovement(0)
    waitmovement(LOCALID_Infernape)
    waitmovement(LOCALID_Samuel)
    goto(Homely_EventScript_Route102_TalkToSamuel)
}

script Homely_EventScript_Route102_TalkToSamuel {
    applymovement(LOCALID_Samuel, Homely_Route102_Samuel_FromFadescreen)
    applymovement(LOCALID_Infernape, Homely_Route102_Infernape_FromFadescreen)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_Route102_Player_FromFadescreen)
    fadescreenspeed(FADE_FROM_BLACK, 4)
    waitmovement(0)
}

script Homely_EventScript_GoToRoom {
    playfanfare(MUS_HEAL)
	waitfanfare
	special(HealPlayerParty)
    clearflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
}

movement Homely_Petalburg_Player_TalkToSamuel_Left {
    walk_down * 4
    walk_right
}

movement Homely_Petalburg_Player_TalkToSamuel_Middle {
    walk_down * 3
    face_right
}

movement Homely_Petalburg_Player_TalkToSamuel_Right {
    walk_down * 2
    walk_left
    walk_down * 2
    face_right
}

movement Homely_Petalburg_Samuel_TalkToSamuel_Left {
    delay_16 * 3
    delay_8
    face_left
    delay_8
}

movement Homely_Petalburg_Samuel_TalkToSamuel_Middle {
    delay_16 * 2
    delay_8
    face_left
    delay_8
}

movement Homely_Petalburg_Samuel_TalkToSamuel_Right {
    delay_16 * 4
    delay_8
    face_left
    delay_8
}

movement Homely_Petalburg_Infernape_TalkToSamuel_Left {
    walk_in_place_fast_down * 4
}

movement Homely_Petalburg_Infernape_TalkToSamuel_Middle {
    walk_in_place_fast_down * 3
}

movement Homely_Petalburg_Infernape_TalkToSamuel_Right {
    walk_in_place_fast_down * 5
}

movement Homely_Petalburg_Player_Leave {
    walk_down * 5
    walk_right
    delay_1
    walk_right * 8
    face_up
}

movement Homely_Petalburg_Samuel_Leave {
    walk_down * 4
    delay_16
    delay_8
    face_down
    delay_8
    walk_right * 8
    face_down
}

movement Homely_Petalburg_Infernape_Leave {
    walk_down * 4
    walk_in_place_down * 2
    walk_down
    delay_1
    walk_right * 7
}

movement Homely_Petalburg_Fadescreen {
    walk_right * 3
}

movement Homely_Petalburg_Camera_Fadescreen_Reset {
    walk_faster_right * 3
}

movement Homely_Route102_Player_FromFadescreen {
    walk_down * 3
    face_right
}

movement Homely_Route102_Samuel_FromFadescreen {
    walk_down * 3
    face_left
}

movement Homely_Route102_Infernape_FromFadescreen {
    walk_down * 3
}

movement Homely_MoveToRoute102_Player {
    walk_down * 13
    walk_left * 12
    walk_down * 2
}

movement Homely_MoveToRoute102_Samuel {
    walk_down * 11
    walk_left * 11
    walk_down * 5
}

movement Homely_MoveToRoute102_Infernape {
    walk_down * 11
    walk_left * 11
    walk_right
    walk_down * 4
}

movement Homely_MoveToRoute102_PositionCamera {
    walk_faster_right * 4
    walk_faster_down * 3
}