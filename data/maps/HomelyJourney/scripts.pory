const LOCALID_Samuel_Petalburg    = 1
const LOCALID_Infernape_Petalburg = 2

const LOCALID_Samuel_Route102     = 3
const LOCALID_Infernape_Route102  = 4

const LOCALID_Samuel_Oldale       = 5
const LOCALID_Infernape_Oldale    = 6

const LOCALID_Samuel_Route101     = 7
const LOCALID_Infernape_Route101  = 8

const LOCALID_Samuel_Borea        = 9
const LOCALID_Infernape_Borea     = 10

const LEFT              = 7
const MIDDLE            = 8
const RIGHT             = 9

mapscripts HomelyJourney_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        gettime
        if (var(VAR_0x8000) >= 21 || var(VAR_0x8000) < 5) { // Night
            initclock(7,0)
        } elif (var(VAR_0x8000) >= 5 && var(VAR_0x8000) < 9) { // Morning
            initclock(13,0)
        } elif (var(VAR_0x8000) >= 17 && var(VAR_0x8000) < 21) { // Evening
            initclock(7,0)
        } else { // Day
            return
        }
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_1, 0 {
            addvar(VAR_TEMP_1, 1)
            setmetatile(8, 6, METATILE_Petalburg_Door_Gym_Closed, TRUE)
            setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
        }
    ]
}

script Homely_EventScript_Trigger {
    setflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
    getplayerxy(VAR_TEMP_A, VAR_TEMP_1)
    if (var(VAR_TEMP_A) == LEFT) {
        applymovement(LOCALID_Samuel_Petalburg, Homely_Petalburg_Samuel_TalkToSamuel_Left)
        applymovement(LOCALID_Infernape_Petalburg, Homely_Petalburg_Infernape_TalkToSamuel_Left)
        applymovement(OBJ_EVENT_ID_PLAYER, Homely_Petalburg_Player_TalkToSamuel_Left)
    } elif (var(VAR_TEMP_A) == MIDDLE) {
        applymovement(LOCALID_Samuel_Petalburg, Homely_Petalburg_Samuel_TalkToSamuel_Middle)
        applymovement(LOCALID_Infernape_Petalburg, Homely_Petalburg_Infernape_TalkToSamuel_Middle)
        applymovement(OBJ_EVENT_ID_PLAYER, Homely_Petalburg_Player_TalkToSamuel_Middle)
    } elif (var(VAR_TEMP_A) == RIGHT) {
        applymovement(LOCALID_Samuel_Petalburg, Homely_Petalburg_Samuel_TalkToSamuel_Right)
        applymovement(LOCALID_Infernape_Petalburg, Homely_Petalburg_Infernape_TalkToSamuel_Right)
        applymovement(OBJ_EVENT_ID_PLAYER, Homely_Petalburg_Player_TalkToSamuel_Right)
    }
    waitmovement(0)
    msgbox(format("SAMUEL: Come on, {PLAYER}.\n We have a nice walk ahead of us."), MSGBOX_DEFAULT)
    closemessage
    goto(Homely_EventScript_Petalburg_Leave)
}

script Homely_EventScript_Petalburg_Leave {
    applymovement(LOCALID_Samuel_Petalburg, Homely_Petalburg_Samuel_Leave)
    applymovement(LOCALID_Infernape_Petalburg, Homely_Petalburg_Infernape_Leave)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_Petalburg_Player_Leave)
    waitmovement(0)
    msgbox(format("SAMUEL: Here we are {PLAYER}, almost at ROUTE 102. Can you remember when\l I sent you on this road with WALLY?\p Developing as a TRAINER…\n Catching so many POKéMON…\l You've come such a long way since then.\p Have you seen WALLY recently?\n How is VERDANTURF TOWN treating him?"), MSGBOX_DEFAULT)
    closemessage
    applymovement(LOCALID_Samuel_Petalburg, Homely_Talk_FacingDown)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_Talk_FacingUp)
    waitmovement(0)
    special(SpawnCameraObject)
    applymovement(LOCALID_Samuel_Petalburg, Homely_Petalburg_Fadescreen)
    applymovement(LOCALID_Infernape_Petalburg, Homely_Petalburg_Fadescreen)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_Petalburg_Fadescreen)
    fadescreenspeed(FADE_TO_BLACK, 4)
    waitmovement(0)
    goto(Homely_EventScript_MoveToRoute102)
}

script Homely_EventScript_MoveToRoute102 {
    setobjectxy(OBJ_EVENT_ID_PLAYER, 8, 30)
    setobjectxy(OBJ_EVENT_ID_FOLLOWER, 8, 29)
    applymovement(OBJ_EVENT_ID_CAMERA, Homely_Camera_PetalburgToRoute102)
    waitmovement(0)
    goto(Homely_EventScript_Route102_TalkToSamuel)
}

script Homely_EventScript_Route102_TalkToSamuel {
    applymovement(LOCALID_Samuel_Route102, Homely_Route102_Samuel_FromFadescreen)
    applymovement(LOCALID_Infernape_Route102, Homely_Route102_Infernape_FromFadescreen)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_Route102_Player_FromFadescreen)
    fadescreenspeed(FADE_FROM_BLACK, 4)
    waitmovement(0)
    msgbox(format("DAD: {PLAYER}, I'm sure you're familiar with roaming in the tall grass like this.\p Maybe we'll encounter some wild POKéMON on our journey back to BOREA GLADE.\p Did you catch anything on this ROUTE?\n I hope to see them in our battle…"), MSGBOX_DEFAULT)
    closemessage
    applymovement(LOCALID_Samuel_Route102, Homely_Talk_FacingLeft)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_Talk_FacingRight)
    waitmovement(0)
    applymovement(LOCALID_Samuel_Route102, Homely_Route102_Samuel_Leave)
    applymovement(LOCALID_Infernape_Route102, Homely_Route102_Infernape_Leave)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_Route102_Player_Leave)
    delay(80)
    fadescreenspeed(FADE_TO_BLACK, 4)
    waitmovement(0)
    goto(Homely_EventScript_MoveToOldale)
}

script Homely_EventScript_MoveToOldale {
    setobjectxy(OBJ_EVENT_ID_PLAYER, 7, 55)
    setobjectxy(OBJ_EVENT_ID_FOLLOWER, 6, 55)
    applymovement(OBJ_EVENT_ID_CAMERA, Homely_Camera_Route102ToOldale)
    waitmovement(0)
    gettime
    if (var(VAR_0x8000) == 7) { // Morning
        initclock(13,0)
    } else { // Day
        initclock(18,0)
    }
    goto(Homely_EventScript_Oldale_TalkToSamuel)
}

script Homely_EventScript_Oldale_TalkToSamuel {
    applymovement(LOCALID_Samuel_Oldale, Homely_Oldale_Samuel_FromFadescreen)
    applymovement(LOCALID_Infernape_Oldale, Homely_Oldale_Infernape_FromFadescreen)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_Oldale_Player_FromFadescreen)
    fadescreenspeed(FADE_FROM_BLACK, 4)
    waitmovement(0)
    msgbox(format("DAD: I sometimes like to make a pit-stop in OLDALE TOWN on the way\l to the PETLABURG CITY GYM.\p They journey isn't too long but I enjoy spending time with the townsfolk here.\p I met and battled a particularly odd couple after doing an INTERVIEW about my new role as PETALBURG GYM LEADER.\p They travel all over HOENN, so you may even run into them at some point."), MSGBOX_DEFAULT)
    closemessage
    applymovement(LOCALID_Samuel_Oldale, Homely_Talk_FacingDown)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_Talk_FacingUp)
    waitmovement(0)
    applymovement(LOCALID_Samuel_Oldale, Homely_Oldale_Samuel_Leave)
    applymovement(LOCALID_Infernape_Oldale, Homely_Oldale_Infernape_Leave)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_Oldale_Player_Leave)
    delay(128)
    fadescreenspeed(FADE_TO_BLACK, 4)
    waitmovement(0)
    goto(Homely_EventScript_MoveToRoute101)
}

script Homely_EventScript_MoveToRoute101 {
    setobjectxy(OBJ_EVENT_ID_PLAYER, 17, 76)
    setobjectxy(OBJ_EVENT_ID_FOLLOWER, 18, 76)
    applymovement(OBJ_EVENT_ID_CAMERA, Homely_Camera_OldaleToRoute101)
    waitmovement(0)
    initclock(18,0) // Always Evening
    goto(Homely_EventScript_Route101_TalkToSamuel)
}

script Homely_EventScript_Route101_TalkToSamuel {
    applymovement(LOCALID_Samuel_Route101, Homely_Route101_Samuel_FromFadescreen)
    applymovement(LOCALID_Infernape_Route101, Homely_Route101_Infernape_FromFadescreen)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_Route101_Player_FromFadescreen)
    fadescreenspeed(FADE_FROM_BLACK, 4)
    waitmovement(0)
    checkitem(ITEM_ESPURR_POKEBALL, 1)
    if (var(VAR_RESULT) == TRUE) {
        call(Homely_EventScript_Route101_TalkToSamuel_Espurr)
    } else {
        call(Homely_EventScript_Route101_TalkToSamuel_Ignatius)
    }
    closemessage
    applymovement(LOCALID_Samuel_Route101, Homely_Talk_FacingUp)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_Talk_FacingDown)
    waitmovement(0)
    applymovement(LOCALID_Samuel_Route101, Homely_Route101_Samuel_Leave)
    applymovement(LOCALID_Infernape_Route101, Homely_Route101_Infernape_Leave)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_Route101_Player_Leave)
    delay(128)
    fadescreenspeed(FADE_TO_BLACK, 4)
    waitmovement(0)
    goto(Homely_EventScript_MoveToBorea)
}

script Homely_EventScript_Route101_TalkToSamuel_Espurr {
    msgbox(format("DAD: We're almost home now, {PLAYER}.\n I am really looking forward to seeing your MOM, DIANCIE & ESPURR.\p I know you must be as well, but this\n has been a nice journey together."), MSGBOX_DEFAULT)
    closemessage
    playmoncry(SPECIES_ESPURR, 0)
    delay(10)
    applymovement(LOCALID_Samuel_Route101, JumpIPUp)
    waitmovement(0)
    waitmoncry
    delay(30)
    applymovement(LOCALID_Samuel_Route101, Question)
    playse(SE_PIN)
    if (flag(FLAG_ESPURR_GENDER) == FALSE) {
        msgbox(format("DAD: Was that--?!\n You have ESPURR with you?!\p Well, I never would've thought that…\n He was always so unadventurous.\p I'm glad he's gotten to see the world by accompanying you on your travels.\p Even if we don't make a battler of him, he can show off how handsome he is,\l at the POKéMON CONTEST HALL."), MSGBOX_DEFAULT)
    } else {
        msgbox(format("DAD: Was that--?!\n You have ESPURR with you?!\p Well, I never would've thought that…\n She was always so unadventurous.\p I'm glad she's gotten to see the world by accompanying you on your travels.\p Even if we don't make a battler of her, she can show off how pretty she is,\l at the POKéMON CONTEST HALL."), MSGBOX_DEFAULT)
    }
    waitmovement(0)
    waitse
}

script Homely_EventScript_Route101_TalkToSamuel_Ignatius {
    msgbox(format("DAD: We're almost home now, {PLAYER}.\n I am really looking forward to seeing your MOM, DIANCIE & ESPURR.\p I know you must be as well, but this\n has been a nice journey together.\p I heard when you first met my friend, PROF. IGNATIUS, that you saved him from a wild POKéMON on this very ROUTE.\p It's amazing to hear that you were so brave, but it's also very funny…\l He and I met in a very similar way."), MSGBOX_DEFAULT)
}

script Homely_EventScript_MoveToBorea {
    setobjectxy(OBJ_EVENT_ID_PLAYER, 11, 98)
    setobjectxy(OBJ_EVENT_ID_FOLLOWER, 11, 97)
    applymovement(OBJ_EVENT_ID_CAMERA, Homely_Camera_Route101ToBorea)
    waitmovement(0)
    initclock(21,0)
    goto(Homely_EventScript_Borea_TalkToSamuel)
}

script Homely_EventScript_Borea_TalkToSamuel {
    applymovement(LOCALID_Samuel_Borea, Homely_Borea_Samuel_FromFadescreen)
    applymovement(LOCALID_Infernape_Borea, Homely_Borea_Infernape_FromFadescreen)
    applymovement(OBJ_EVENT_ID_PLAYER, Homely_Borea_Player_FromFadescreen)
    fadescreenspeed(FADE_FROM_BLACK, 4)
    waitmovement(0)
}

script Homely_EventScript_GoToRoom {
    playfanfare(MUS_HEAL)
	waitfanfare
	special(HealPlayerParty)
    clearflag(FLAG_SAFE_FOLLOWER_MOVEMENT)
}

movement Homely_Petalburg_Player_TalkToSamuel_Left {
    walk_down * 4
    walk_right
}

movement Homely_Petalburg_Player_TalkToSamuel_Middle {
    walk_down * 3
    face_right
}

movement Homely_Petalburg_Player_TalkToSamuel_Right {
    walk_down * 2
    walk_left
    walk_down * 2
    face_right
}

movement Homely_Petalburg_Samuel_TalkToSamuel_Left {
    delay_16 * 3
    delay_8
    face_left
    delay_8
}

movement Homely_Petalburg_Samuel_TalkToSamuel_Middle {
    delay_16 * 2
    delay_8
    face_left
    delay_8
}

movement Homely_Petalburg_Samuel_TalkToSamuel_Right {
    delay_16 * 4
    delay_8
    face_left
    delay_8
}

movement Homely_Petalburg_Infernape_TalkToSamuel_Left {
    walk_in_place_fast_down * 4
}

movement Homely_Petalburg_Infernape_TalkToSamuel_Middle {
    walk_in_place_fast_down * 3
}

movement Homely_Petalburg_Infernape_TalkToSamuel_Right {
    walk_in_place_fast_down * 5
}

movement Homely_Talk_FacingUp {
    walk_in_place_up
    delay_16
    walk_in_place_up
    delay_16
}

movement Homely_Talk_FacingDown {
    delay_8
    walk_in_place_down
    delay_16
    walk_in_place_down
    delay_8
}

movement Homely_Talk_FacingLeft {
    walk_in_place_left
    delay_16
    walk_in_place_left
    delay_16
}

movement Homely_Talk_FacingRight {
    delay_8
    walk_in_place_right
    delay_16
    walk_in_place_right
    delay_8
}

movement Homely_Petalburg_Player_Leave {
    walk_down * 5
    walk_right
    delay_1
    walk_right * 8
    face_up
}

movement Homely_Petalburg_Samuel_Leave {
    walk_down * 4
    delay_16
    delay_8
    face_down
    delay_8
    walk_right * 8
    face_down
}

movement Homely_Petalburg_Infernape_Leave {
    walk_down * 4
    walk_in_place_down * 2
    walk_down
    delay_1
    walk_right * 7
}

movement Homely_Petalburg_Fadescreen {
    walk_right * 3
}

movement Homely_Petalburg_Camera_Fadescreen_Reset {
    walk_faster_right * 3
}

movement Homely_Route102_Player_FromFadescreen {
    walk_down * 3
    face_right
}

movement Homely_Route102_Samuel_FromFadescreen {
    walk_down * 3
    face_left
}

movement Homely_Route102_Infernape_FromFadescreen {
    walk_down * 3
}

movement Homely_Camera_PetalburgToRoute102 {
    walk_faster_down * 19
    walk_faster_left * 7
}

movement Homely_Route102_Player_Leave {
    walk_down * 3
    walk_right * 5
}

movement Homely_Route102_Samuel_Leave {
    walk_down * 2
    delay_16 * 2
    walk_right * 5
}

movement Homely_Route102_Infernape_Leave {
    walk_down * 2
    walk_in_place_down * 2
    walk_down
    walk_right * 4
}

movement Homely_Camera_Route102ToOldale {
    walk_faster_right
    walk_faster_down * 21
}

movement Homely_Oldale_Player_FromFadescreen {
    walk_right * 3
    face_up
}

movement Homely_Oldale_Samuel_FromFadescreen {
    walk_right * 3
    face_down
}

movement Homely_Oldale_Infernape_FromFadescreen {
    walk_right * 3
}

movement Homely_Oldale_Player_Leave {
    walk_right * 3
    delay_16 * 2
    walk_down * 6
}

movement Homely_Oldale_Samuel_Leave {
    walk_right * 4
    walk_down * 7
}

movement Homely_Oldale_Infernape_Leave {
    walk_right * 5
    walk_down * 6
}

movement Homely_Camera_OldaleToRoute101 {
    walk_faster_left
    walk_faster_down * 22
}

movement Homely_Route101_Player_FromFadescreen {
    walk_left * 3
    face_down
}

movement Homely_Route101_Samuel_FromFadescreen {
    walk_left * 3
    face_up
}

movement Homely_Route101_Infernape_FromFadescreen {
    walk_left * 3
}

movement Homely_Route101_Player_Leave {
    walk_left * 8
    walk_down * 4
}

movement Homely_Route101_Samuel_Leave {
    walk_left * 7
    delay_16 * 2
    walk_down * 3
}

movement Homely_Route101_Infernape_Leave {
    walk_left * 7
    walk_in_place_left * 2
    walk_left
    walk_down * 2
}

movement Homely_Camera_Route101ToBorea {
    walk_faster_right * 2
    walk_faster_down * 25
}

movement Homely_Borea_Player_FromFadescreen {
    walk_down * 3
    face_right
}

movement Homely_Borea_Samuel_FromFadescreen {
    walk_down * 3
    face_left
}

movement Homely_Borea_Infernape_FromFadescreen {
    walk_down * 3
}