const LOCALID_BrendanMom = 1
const LOCALID_BrendanDad = 2
const LOCALID_BrendanDiancie = 3
const LOCALID_BrendanInfernape = 4
const LOCALID_BrendanEspurr = 5

const LOCALID_MayMom = 6
const LOCALID_MayDad = 7
const LOCALID_MayDiancie = 8
const LOCALID_MayInfernape = 9
const LOCALID_MayEspurr = 10

mapscripts PlayerHouses_MapScripts {
    MAP_SCRIPT_ON_TRANSITION {
        initclock(18,30)
        call(PlayerHouses_MoveDadToKitchen)
    }
    MAP_SCRIPT_ON_FRAME_TABLE [
        VAR_TEMP_0, 0 {
            setvar(VAR_TEMP_0, 1)
            call(PlayerHouses_SetVariables)
            if (flag(FLAG_SET_HOMELY_STATE) == TRUE) {
                goto(Homely_PlayerHouses_StartDinner)
            }
        }
    ]
}

script PlayerHouses_SetVariables {
    checkplayergender
    if (var(VAR_RESULT) == MALE) {
        setvar(VAR_TEMP_1, LOCALID_BrendanMom)
        setvar(VAR_TEMP_2, LOCALID_BrendanDad)
        setvar(VAR_TEMP_3, LOCALID_BrendanDiancie)
        setvar(VAR_TEMP_4, LOCALID_BrendanInfernape)
        setvar(VAR_TEMP_5, LOCALID_BrendanEspurr)
    } else {
        setvar(VAR_TEMP_1, LOCALID_MayMom)
        setvar(VAR_TEMP_2, LOCALID_MayDad)
        setvar(VAR_TEMP_3, LOCALID_MayDiancie)
        setvar(VAR_TEMP_4, LOCALID_MayInfernape)
        setvar(VAR_TEMP_5, LOCALID_MayEspurr)
    }
}

script PlayerHouses_MoveDadToKitchen {
    setobjectxyperm(LOCALID_BrendanDad, 2, 3)
    setobjectmovementtype(LOCALID_BrendanDad, MOVEMENT_TYPE_WALK_IN_PLACE_UP)
    setobjectxyperm(LOCALID_MayDad, 38, 3)
    setobjectmovementtype(LOCALID_MayDad, MOVEMENT_TYPE_WALK_IN_PLACE_UP)
}

script Homely_PlayerHouses_StartDinner {
    special(SpawnCameraObject)
    checkplayergender
    if (var(VAR_RESULT) == MALE) {
        applymovement(OBJ_EVENT_ID_PLAYER, Homely_Movement_BrendansHouse_WalkToTable)
        applymovement(OBJ_EVENT_ID_CAMERA, Homely_Movement_BrendansHouse_CameraWalkToTable)
    } else {
        applymovement(OBJ_EVENT_ID_PLAYER, Homely_Movement_MaysHouse_WalkToTable)
        applymovement(OBJ_EVENT_ID_CAMERA, Homely_Movement_MaysHouse_CameraWalkToTable)
    }
    waitmovement(OBJ_EVENT_ID_PLAYER)
    msgbox(format("DAD: Great timing, {PLAYER}.\n I'm bringing dinner over now!"), MSGBOX_DEFAULT)
    closemessage
    delay(30)
    msgbox(format("MOM: You can tell us everything\n that happened in BOREA PASSAGE!\p What is like exploring right beneath us,\n in the DEVON CORP.'s latest project?"), MSGBOX_DEFAULT)
    closemessage
    checkplayergender
    if (var(VAR_RESULT) == MALE) {
        applymovement(VAR_TEMP_2, Homely_Movement_BrendansHouse_DadWalkToTable)
    } else {
        applymovement(VAR_TEMP_2, Homely_Movement_MaysHouse_DadWalkToTable)
    }
    waitmovement(0)
    delay(30)
    special(TurnOnTVScreen)
    checkplayergender
    if (var(VAR_RESULT) == MALE) {
        applymovement(VAR_TEMP_2, WalkIPRight)
    } else {
        applymovement(VAR_TEMP_2, WalkIPLeft)
    }
    delay(60)
    checkplayergender
    if (var(VAR_RESULT) == MALE) {
        applymovement(VAR_TEMP_1, JumpIPRight)
        applymovement(VAR_TEMP_2, JumpIPRight)
    } else {
        applymovement(VAR_TEMP_1, JumpIPLeft)
        applymovement(VAR_TEMP_2, JumpIPLeft)
    }
    msgbox(format("MOM & DAD: Time to eat!"), MSGBOX_DEFAULT)
    waitmovement(0)
    closemessage
    delay(60)
    fadescreen(FADE_TO_BLACK)
	playfanfare(MUS_DP_HEAL)
	waitfanfare
	special(HealPlayerParty)
	fadescreen(FADE_FROM_BLACK)
    goto(Homely_PlayerHouses_AfterDinner)
}

script Homely_PlayerHouses_AfterDinner {
    delay(30)
    special(TurnOffTVScreen)
    checkplayergender
    if (var(VAR_RESULT) == MALE) {
        applymovement(VAR_TEMP_2, WalkIPRight)
    } else {
        applymovement(VAR_TEMP_2, WalkIPLeft)
    }
    delay(30)
    msgbox(format("DAD: Wow!\n So that's what happened down there.\p Even prepared, I'm relieved that you didn't need battle down there.\l Especially against someone like…\p What was his name again?"), MSGBOX_DEFAULT)
    closemessage
    delay(30)
    msgbox(format("MOM: His name was ARCHIE, dear…\p But anyway, he wouldn't be any trouble for a LABRADORITE BADGE holder!\l Isn't that right, {PLAYER}!"), MSGBOX_DEFAULT)
    closemessage
    delay(30)
    applymovement(VAR_TEMP_2, Exclaim)
    playse(SE_PIN)
    msgbox(format("DAD: Well, you're not wrong there!\p I could still give {PLAYER} a run for their money, if I wanted to that is."), MSGBOX_DEFAULT)
    waitmovement(0)
    waitse
    closemessage
    delay(30)
    if (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == FALSE) {
        msgbox(format("MOM: Oh stop it, you know that you\n got beaten fair and square.\p It was so fun to watch.\n Even PEDRO loved it!"), MSGBOX_DEFAULT)
    } elif (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == TRUE) {
        msgbox(format("MOM: Oh stop it, you know that you\n got beaten fair and square.\p It was so fun to watch.\n Even POPPY loved it!"), MSGBOX_DEFAULT)
    } else {
        msgbox(format("MOM: Oh stop it, you know that you\n got beaten fair and square.\p It was so fun to watch.\n Even ESPURR loved it!"), MSGBOX_DEFAULT)
    }
    closemessage
    delay(30)
    bufferleadmonspeciesname(STR_VAR_1)
    if (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == FALSE) {
        msgbox(format("DAD: Speaking about our battle,\n it seemed PEDRO really enjoyed\l seeing {STR_VAR_1} in action.\p I think he would even like to have\n his own attempt at a POKéMON battle.\p PEDRO has had a weird energy recently,\n why don't you see what he thinks."), MSGBOX_DEFAULT)
    } elif (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == TRUE) {
        msgbox(format("DAD: Speaking about our battle,\n it seemed POPPY really enjoyed\l seeing {STR_VAR_1} in action.\p I think she would even like to have\n her own attempt at a POKéMON battle.\p POPPY has had a weird energy recently,\n why don't you see what she thinks."), MSGBOX_DEFAULT)
    } else {
        msgbox(format("DAD: Speaking about our battle,\n it seemed ESPURR really enjoyed\l seeing {STR_VAR_1} in action.\p I think it would even like to have\n its own attempt at a POKéMON battle.\p ESPURR has had a weird energy recently,\n why don't you see what it thinks."), MSGBOX_DEFAULT)
    }
    closemessage
    checkplayergender
    if (var(VAR_RESULT) == MALE) {
        setvar(VAR_0x8006, MOVEMENT_TYPE_FACE_RIGHT)
    } else {
        setvar(VAR_0x8006, MOVEMENT_TYPE_FACE_LEFT)
    }
    copyvar(VAR_0x8005, VAR_TEMP_2)
    special(SetHomelyDadObjectMovementType)
    special(MoveHomelyCameraObject)
    completesubquest(HOMELY, ESPURR)
}

script Homely_PlayerHouses_Parents {
    special(GetOverworldObject)
    if (var(VAR_RESULT) == OBJ_EVENT_GFX_MOM) {
        goto(Homely_PlayerHouses_Mom)
    } elif (var(VAR_RESULT) == OBJ_EVENT_GFX_NORMAN) {
        goto(Homely_PlayerHouses_Dad)
    }
}

script Homely_PlayerHouses_Mom {
    faceplayer
    if (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == FALSE) {
        msgbox(format("MOM: Why don't you go see\n how PEDRO is doing, honey?"), MSGBOX_DEFAULT)
    } elif (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == TRUE) {
        msgbox(format("MOM: Why don't you go see\n how POPPY is doing, honey?"), MSGBOX_DEFAULT)
    } else {
        msgbox(format("MOM: Why don't you go see\n how ESPURR is doing, honey?"), MSGBOX_DEFAULT)
    }
    closemessage
    applymovement(LOCALID_BrendanMom, FaceRight)
    applymovement(LOCALID_MayMom, FaceLeft)
}

script Homely_PlayerHouses_Dad {
    faceplayer
    if (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == FALSE) {
        msgbox(format("DAD: You should go and see if PEDRO is interested in learning how to battle."), MSGBOX_DEFAULT)
    } elif (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == TRUE) {
        msgbox(format("DAD: You should go and see if POPPY is interested in learning how to battle."), MSGBOX_DEFAULT)
    } else {
        msgbox(format("DAD: You should go and see if ESPURR is interested in learning how to battle."), MSGBOX_DEFAULT)
    }
    closemessage
    applymovement(LOCALID_BrendanDad, FaceRight)
    applymovement(LOCALID_MayDad, FaceLeft)
}

script Homely_PlayerHouses_Espurr {
    playmoncry(SPECIES_ESPURR, 0)
    getpartysize
    bufferleadmonspeciesname(STR_VAR_1)
    if (var(VAR_RESULT) == PARTY_SIZE) {
        goto(Homely_PlayerHouses_EspurrFullParty)
    } else {
        goto(Homely_PlayerHouses_EspurrJoin)
    }
}

script Homely_PlayerHouses_EspurrFullParty {
    if (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == FALSE) {
        msgbox(format("PEDRO looks determined to start\n battling alongside {STR_VAR_1}.\p I should prepare a space in my\n party so he can join."), MSGBOX_DEFAULT)
    } elif (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == TRUE) {
        msgbox(format("POPPY looks determined to start\n battling alongside {STR_VAR_1}.\p I should prepare a space in my\n party so she can join."), MSGBOX_DEFAULT)
    } else {
        msgbox(format("ESPURR looks determined to start\n battling alongside {STR_VAR_1}.\p I should prepare a space in my\n party so it can join."), MSGBOX_DEFAULT)
    }
    closemessage
}

script Homely_PlayerHouses_EspurrJoin {
    if (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == FALSE) {
        msgbox(format("PEDRO looks determined to start\n battling alongside {STR_VAR_1}.\l Should he join your party?"), MSGBOX_YESNO)
    } elif (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == TRUE) {
        msgbox(format("POPPY looks determined to start\n battling alongside {STR_VAR_1}.\l Should she join your party?"), MSGBOX_YESNO)
    } else {
        msgbox(format("ESPURR looks determined to start\n battling alongside {STR_VAR_1}.\l Should it join your party?"), MSGBOX_YESNO)
    }
    while (var(VAR_RESULT) == NO) {
        if (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == FALSE) {
            msgbox(format("Are you sure?\n Should PEDRO join your party?"), MSGBOX_YESNO)
        } elif (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == TRUE) {
            msgbox(format("Are you sure?\n Should POPPY join your party?"), MSGBOX_YESNO)
        } else {
            msgbox(format("Are you sure?\n Should ESPURR join your party?"), MSGBOX_YESNO)
        }
    }
    closemessage
    playfanfare(MUS_DP_EVOLVED)
    msgbox(format("ESPURR joined you on your adventure!"), MSGBOX_DEFAULT)
    waitfanfare
    delay(20)
    closemessage
    setflag(FLAG_HIDE_BRENDANS_HOUSE_ESPURR)
    setflag(FLAG_HIDE_MAYS_HOUSE_ESPURR)
    givemon(SPECIES_ESPURR, 20)
    goto(Homely_PlayerHouses_EspurrEvolve)
}

script Homely_PlayerHouses_EspurrEvolve {
    if (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == FALSE) {
        call(Homely_PlayerHouses_EspurrPreEvolution)
        setvar(VAR_0x8001, SPECIES_MEOWSTIC_MALE)
    } elif (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == TRUE) {
        call(Homely_PlayerHouses_EspurrPreEvolution)
        setvar(VAR_0x8001, SPECIES_MEOWSTIC_FEMALE)
    } else {
        goto(Homely_PlayerHouses_EspurrChooseGender)
    }
    callnative(EvolvePartyMonToSpecies)
    removeobject(VAR_TEMP_5)
    applymovement(VAR_TEMP_1, FaceRight)
    applymovement(VAR_TEMP_2, FaceRight)
    applymovement(OBJ_EVENT_ID_PLAYER, FaceLeft)
    waitmovement(0)
    delay(20)
    msgbox(format("DAD: Wow!\p ESPURR just evolved into a MEOWSTIC!\p Purely out of love for you, {PLAYER}!"), MSGBOX_DEFAULT)
    delay(30)
    msgbox(format("MOM: That is truly amazing!\p I'll leave you to officially\n give MEOWSTIC a name."), MSGBOX_DEFAULT)
    closemessage
    copyvar(VAR_0x8004, VAR_TEMP_F)
    call(Common_EventScript_NameReceivedPartyMon)
    goto(Homely_PlayerHouses_DinnerEnd)
}

const POPPY = 0
const PEDRO = 1

script Homely_PlayerHouses_EspurrChooseGender {
    applymovement(VAR_TEMP_1, FaceRight)
    applymovement(VAR_TEMP_2, FaceRight)
    applymovement(OBJ_EVENT_ID_PLAYER, FaceLeft)
    waitmovement(0)
    msgbox(format("MOM: Wow, {PLAYER}!\p I'm surprised such a shy POKéMON decided to go with you.\p I've been calling ESPURR “NORTI TORTI”,\n but we need to settle on a name.\l What do you think?"), MSGBOX_DEFAULT)
	multichoice(4, 8, MULTI_POPPY_PEDRO, TRUE)
	if (var(VAR_RESULT) == POPPY) {
		setflag(FLAG_ESPURR_GENDER)
		msgbox(format("MOM: I knew it, that's the most wonderful name for her.\p ESPURR is so very shy, but I just know some adventure would do her good…"), MSGBOX_DEFAULT)
	} elif (var(VAR_RESULT) == PEDRO) {
		msgbox(format("MOM: I knew it, that's the most wonderful name for him.\p ESPURR is so very shy, but I just know some adventure would do him good…"), MSGBOX_DEFAULT)
	}
	setflag(FLAG_CHOSEN_ESPURR_GENDER)
    goto(Homely_PlayerHouses_EspurrEvolve)
}

script Homely_PlayerHouses_EspurrPreEvolution {
    call(Common_EventScript_GetGiftMonPartySlot)
    copyvar(VAR_TEMP_F, VAR_0x8004)
    playse(SE_PIN)
    applymovement(OBJ_EVENT_ID_PLAYER, Exclaim)
    msgbox(format("… … … … … …\n Oh?\l What's this?"), MSGBOX_DEFAULT)
    delay(20)
    closemessage
    copyvar(VAR_0x8000, VAR_TEMP_F)
    setvar(VAR_0x8002, FALSE)
}

script Homely_PlayerHouses_DinnerEnd {
    
}

script Homely_PlayerHouses_DoorTrigger {
    delay(30)
    applymovement(OBJ_EVENT_ID_PLAYER, WalkUp)
    if (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == FALSE) {
        msgbox(format("I should probably talk to\n PEDRO before I leave."), MSGBOX_DEFAULT)
    } elif (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == TRUE) {
        msgbox(format("I should probably talk to\n POPPY before I leave."), MSGBOX_DEFAULT)
    } else {
        msgbox(format("I should probably talk to\n ESPURR before I leave."), MSGBOX_DEFAULT)
    }
    waitmovement(0)
    closemessage
}

script Homely_PlayerHouses_RoomTrigger {
    delay(30)
    applymovement(OBJ_EVENT_ID_PLAYER, WalkDown)
    if (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == FALSE) {
        msgbox(format("I should probably talk to\n PEDRO before I leave."), MSGBOX_DEFAULT)
    } elif (flag(FLAG_CHOSEN_ESPURR_GENDER) == TRUE && flag(FLAG_ESPURR_GENDER) == TRUE) {
        msgbox(format("I should probably talk to\n POPPY before I leave."), MSGBOX_DEFAULT)
    } else {
        msgbox(format("I should probably talk to\n ESPURR before I leave."), MSGBOX_DEFAULT)
    }
    waitmovement(0)
    closemessage
}

movement Homely_Movement_BrendansHouse_WalkToTable {
    walk_up * 2
    walk_left * 3
    delay_16
    walk_in_place_left
}

movement Homely_Movement_MaysHouse_WalkToTable {
    walk_up * 2
    walk_right * 4
    delay_16
    walk_in_place_right
}

movement Homely_Movement_BrendansHouse_CameraWalkToTable {
    walk_up * 2
    walk_diag_northwest
}

movement Homely_Movement_MaysHouse_CameraWalkToTable {
    walk_diag_northeast * 3
    walk_right * 3
}

movement Homely_Movement_BrendansHouse_DadWalkToTable {
    walk_left
    walk_down * 2
    walk_right * 2
    face_down
    delay_16
    walk_in_place_down * 2
    delay_16
    walk_right
    face_down
    delay_16
    walk_in_place_down
    delay_16
    walk_left * 3
    walk_down * 2
    walk_right
    delay_16
    walk_in_place_right
}

movement Homely_Movement_MaysHouse_DadWalkToTable {
    walk_down * 2
    walk_left * 2
    face_down
    delay_16
    walk_in_place_down * 2
    delay_16
    walk_left
    face_down
    delay_16
    walk_in_place_down
    delay_16
    walk_right * 3
    walk_down * 2
    walk_left
    delay_16
    walk_in_place_left
}

movement Homely_Movement_BrendansHouse_PlayerWalkToEspurr {
    walk_up
    walk_right * 7
}

movement Homely_Movement_MaysHouse_PlayerWalkToEspurr {
    walk_up
    walk_right * 8
}
