script Global_FlyingTaxi {

    if ((flag(FLAG_SYS_DEMO_MODE) == TRUE)) { // Demo Restricted Content
        setvar(VAR_TEMP_A, 1)
        goto(FlyingTaxiUnav)

    } elif (var(VAR_FLYING_TAXI) == 0) {
        goto(FlyingTaxiUnav)
    } elif (var(VAR_FLYING_TAXI) == 1) {
        goto(FlyingTaxiFirst)
    } else {
        goto(FlyingTaxiAv)
    }
}

script FlyingTaxiUnav {
    lock
    faceplayer
    msgbox(format("Hello {PLAYER}.\p I have some bad news. Unfortunately, until I get word from the boss,\l the FLYING TAXI service is unavailable."), MSGBOX_DEFAULT)
    goto(FlyingTaxiUnavReason)
}

script FlyingTaxiUnavReason {
    switch (var(VAR_TEMP_A)) {
        case 0:
            random(11)
            copyvar (VAR_TEMP_A, VAR_RESULT)
            addvar (VAR_TEMP_A, 1)
            goto(FlyingTaxiUnavReason)
        case 1:
        case 2:
            msgbox(format("Our SWELLOW are feeling extremely tired and they need a break."), MSGBOX_NPC)
        case 3:
        case 4:
            msgbox(format("There are some powerful winds that our SWELLOW don't want to fly through."), MSGBOX_NPC)
        case 5:
        case 6:
            msgbox(format("Our SWELLOW have become a bit sick,\n we can't risk their wellbeing."), MSGBOX_NPC)
        case 7:
        case 8:
            msgbox(format("Our SWELLOW are nesting. We have to let them take care of their young."), MSGBOX_NPC)
        case 9:
            questmenu(QUEST_MENU_CHECK_UNLOCKED, TEAM_MAGMA)
            if (var(VAR_RESULT) == TRUE) {
                msgbox(format("There's some TEAM MAGMA activity. We\n don't want to put our SWELLOW at risk."), MSGBOX_NPC)
            } else {
                setvar(VAR_TEMP_A, 1)
                goto(FlyingTaxiUnav)
            }
        case 10:
        questmenu(QUEST_MENU_CHECK_UNLOCKED, TEAM_AQUA)
            if (var(VAR_RESULT) == TRUE) {
                msgbox(format("There's some TEAM AQUA activity. We\n don't want to put our SWELLOW at risk."), MSGBOX_NPC)
            } else {
                setvar(VAR_TEMP_A, 1)
                goto(FlyingTaxiUnav)
            }
        case 11:
            msgbox(format("There are some protests going on and they're preventing us from flying.\p I swear we treat our SWELLOW well, they love to stretch their wings and only carry passengers if they're happy to."), MSGBOX_NPC)
        case 12:
            msgbox(format("Our SWELLOW don't want to fly anywhere,\n they refuse to do so during the DEMO."), MSGBOX_NPC)
    }
}


script FlyingTaxiFirst {
    msgbox(format("Hello! You must be {PLAYER}!\n Welcome to the FLYING TAXI service.\p … … …\n How do I know who you are?\p Why your father is the GYM LEADER of my hometown, PETALBURG CITY. And your MOM's shop is one of my favourites!\p When she asked us FLYING TAXI riders to look out for you, we were more than happy to oblidge.\p We can help you revisit any town or city you've been to before.\l All you have to do is ask!"), MSGBOX_NPC)
    addvar (VAR_FLYING_TAXI, 1)
}

script FlyingTaxiAv {
    lock
    faceplayer
    msgbox(format("Hello {PLAYER}!\p I can call you a FLYING TAXI if there's\n somewhere you would like to go.\l Any place that you have in mind?"), MSGBOX_YESNO)
        if (var(VAR_RESULT) == 1) {
            msgbox(format("Where would you like to go?"), MSGBOX_NPC)
            setvar(VAR_0x800A, LAST_TALKED_TO_FLYING_TAXI)
            special(FieldLoadFlyMap)
        } else {
            msgbox(format("Not to worry. Let us know if you ever need to go somewhere in a hurry!"), MSGBOX_NPC)
        }
}